!include "MUI2.nsh"
!include "WinMessages.nsh"

Name "Matrix Mariner GPS"
OutFile "MMGSetup-%s.exe"
InstallDir $PROGRAMFILES32\MMGPS

!insertmacro MUI_PAGE_WELCOME
!insertmacro MUI_PAGE_LICENSE "exe.win32-2.7\rc\windowslicense.txt"
!insertmacro MUI_PAGE_DIRECTORY
!insertmacro MUI_PAGE_INSTFILES
!insertmacro MUI_PAGE_FINISH

!insertmacro MUI_UNPAGE_WELCOME
!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES
!insertmacro MUI_UNPAGE_FINISH

!insertmacro MUI_LANGUAGE "English"

Section "Matrix Mariner GPS"
;;unregister font that may be previously registered so files can copy over
System::Call "GDI32::RemoveFontResourceA(t) i ('$INSTDIR\MMG\rc\LCDDotMatrix5x8.ttf') .s"
Pop $0
SendMessage ${HWND_BROADCAST} ${WM_FONTCHANGE} 0 0

SetOutPath "$INSTDIR\MMG"
File /r "exe.win32-2.7\*"

SetShellVarcontext all
CreateDirectory "$APPDATA\Matrix Mariner"
CopyFiles "$INSTDIR\MMG\rc\mmg.ico" "$APPDATA\Matrix Mariner"
CreateShortCut "$INSTDIR\MMG\MMG.lnk" "$INSTDIR\MMG\MMG.exe" "" \
"$APPDATA\Matrix Mariner\mmg.ico" "" SW_SHOWNORMAL "" "Matrix Mariner GPS Display and tool"

CreateDirectory "$SMPROGRAMS\Matrix Mariner"
CopyFiles "$INSTDIR\MMG\MMG.lnk" "$SMPROGRAMS\Matrix Mariner"

WriteUninstaller "$INSTDIR\Uninstall.exe"
CreateShortCut "$INSTDIR\MMG\Uninstall.lnk" "$INSTDIR\Uninstall.exe" "" \
"$INSTDIR\Uninstall.exe" "" SW_SHOWNORMAL "" "Uninstall Matrix Mariner GPS"
CopyFiles "$INSTDIR\MMG\Uninstall.lnk" "$SMPROGRAMS\Matrix Mariner"

SetShellVarContext current
CopyFiles "$INSTDIR\MMG\MMG.lnk" "$DESKTOP"

;;register font
System::Call "GDI32::AddFontResourceA(t) i ('$INSTDIR\MMG\rc\LCDDotMatrix5x8.ttf') .s"
Pop $0
SendMessage ${HWND_BROADCAST} ${WM_FONTCHANGE} 0 0
SectionEnd

Section "Uninstall"
;;unregister font
System::Call "GDI32::RemoveFontResourceA(t) i ('$INSTDIR\MMG\rc\LCDDotMatrix5x8.ttf') .s"
Pop $0
SendMessage ${HWND_BROADCAST} ${WM_FONTCHANGE} 0 0

RMDir /r "$INSTDIR"
SetShellVarContext all
RMDir /r "$SMPROGRAMS\Matrix Mariner"
RMDir /r "$APPDATA\Matrix Mariner"
SetShellVarContext current
Delete "$DESKTOP\MMG.lnk"

SectionEnd
